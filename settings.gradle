rootProject.name = 'osh-android'
def oshMainDir = "$rootDir/.."

// include builds from other repos
def repos = [
        'osh-core':['lib-swe-common', 'lib-sensorml', 'lib-ows', 'sensorhub-core', 'sensorhub-service-swe', 'sensorhub-storage-perst'],
        'osh-addons/persistence':['sensorhub-storage-h2'],
        'osh-addons/comm':['sensorhub-comm-ble', 'sensorhub-comm-ble-dbus'],
        'osh-addons/sensors/health':['sensorhub-driver-angelsensor'],
        'osh-addons/sensors/video':['sensorhub-driver-videocam', 'sensorhub-service-video'],
        'osh-addons/sensors/positioning':['sensorhub-driver-trupulse'],
        'osh-addons/processing':['sensorhub-process-vecmath', 'sensorhub-process-geoloc', 'sensorhub-process-basicmath']]

repos.each { repoName, modules ->
  File repoDir = new File(oshMainDir, repoName).getCanonicalFile()
  if (!repoDir.exists())
    throw new GradleException('Repository ' + repoName + ' is missing. Please clone it from https://github.com/opensensorhub/' + repoName)
  modules.each { moduleName ->
    File moduleDir = new File(repoDir, moduleName).getCanonicalFile()
    // include all folders with build.gradle as subprojects
    FileTree subprojects = fileTree(moduleDir).include('**/build.gradle')
    subprojects.files.each { File f ->
      File projectFolder = f.parentFile
      String projectName = projectFolder.name
      include projectName
      project(':' + projectName).projectDir = projectFolder
    }
  }
}

// include all folders with build.gradle as subprojects
FileTree subprojects = fileTree(rootDir).include('**/build.gradle')
subprojects.files.each { File f ->
  File projectFolder = f.parentFile
  if (projectFolder != rootDir) {
    String projectName = projectFolder.name
    include projectName
    project(':' + projectName).projectDir = projectFolder
  }
}

